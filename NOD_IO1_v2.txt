[Техническое задание - Определение НОД всех элементов массива]
[Используется Initial Orders 1]
[Результат (НОД всех элементов массива) записывается в ячейку 0]
[Длина массива задаётся в ячейке 98]
[Адрес первой ячейки массива задается в ячейке 99]

[31]	T 108 S [Адрес ячейки, следующей за последней инструкцией программы]

[32]	Z 0 S [Останов для отладки]
[33]	T 0 S [Обнуление аккумулятора]

[В ячейке 1 хранится количество необработанных пар чисел (счетчик цикла)]
[Инициализация счетчика цикла]
[34]	A 98 [<len>] S [загрузка в аккумулятор длины массива]
[35]	S 97 [<c1>] S [вычитаем единицу, т.к. количество пар чисел на 1 меньше, чем длина массива]
[36]	T 1 S [запись значения в ячейку 1]

[Инициализация адресных полей изменяемых инструкций]
[37]	A 99 [<addr>] S [Загрузка в аккумулятор адреса 0-го элемента массива]
[38]	L 0 L [Сдвиг аккумулятора на 1 разряд влево]
[39]	A 61 [<r1>] S [Прибавляем код инструкции с нулевым полем адреса] [r1: A 0 S]
[40]	U 61 [<r1>] S [Запись сформированной инструкции без обнуления аккумулятора]
[41]	T 94 [<r1>] S [Запись сформированной инструкции с обнулением аккумулятора]

[42]	A 99 [<addr>] S [Загрузка в аккумулятор адреса 0-го элемента массива]
[43]	L 0 L [Сдвиг аккумулятора на 1 разряд влево]
[44]	A 62 [<r2>] S [Прибавляем код инструкции с полем адреса 1] [r2: S 1 S]
[45]	U 62 [<r2>] S [Запись сформированной инструкции без обнуления аккумулятора]
[46]	T 69 [<r2>] S [Запись сформированной инструкции с обнулением аккумулятора]

[47]	A 99 [<addr>] S [Загрузка в аккумулятор адреса 0-го элемента массива]
[48]	L 0 L [Сдвиг аккумулятора на 1 разряд влево]
[49]	A 64 [<w1>] S [Прибавляем код инструкции с нулевым полем адреса] [w1: U 0 S]
[50]	T 64 [<w1>] S [Запись сформированной инструкции с обнулением аккумулятора]

[51]	A 99 [<addr>] S [Загрузка в аккумулятор адреса 0-го элемента массива]
[52]	L 0 L [Сдвиг аккумулятора на 1 разряд влево]
[53]	A 68 [<w2>] S [Прибавляем код инструкции с полем адреса 1] [w2: T 1 S]
[54]	U 68 [<w2>] S [Запись сформированной инструкции без обнуления аккумулятора]
[55]	T 70 [<w2>] S [Запись сформированной инструкции с обнулением аккумулятора]

[Рабочий цикл]
[loop:]
[56]	A 1 S [Загружаем в аккумулятор счетчик необработанных пар]
[57]	S 97 [<c1>] S [Уменьшаем на 1]
[58]	G 93 [<end>] S [Если результат меньше 0, идем на останов]
[59]	T 1 S [Обновляем значение счетчика в ячейке]

[Поиск НОД для пары чисел]
[iter:]
[60]	T 0 S [обнуляем аккумулятор]
[61]	A 0 S [r1:] [ добавляем к аккумулятору первое число ]
[62] 	S 1 S [r2:] [ вычитаем из аккумулятора второе число ]
[63]	G 68 [<invert>] S [ если результат отрицательный (первое число меньше второго), то идем к <invert> ]
[64]	U 0 S [w1:] [ иначе в ячейку с первым числом кладем разность чисел, сохраняя ее в аккумуляторе ]
[65]	S 97 [<c1>] S [ вычитаем из разности единицу и результат кладем в аккумулятор ]
[66]	E 60 [<iter>] S [ если результат >=0, значит разность была >0, т.е. числа разные –> идем на следующую итерацию ]
[67]	G 72 [<exit>] S [ числа одинаковые, НОД найден – выходим из цикла поиска НОД для пары чисел ]
[invert:]
[68]	T 1 S [w2:] [ в ячейку со вторым числом кладем отрицательную разность, обнуляя аккумулятор ]
[69]	S 1 S [r2:] [ вычитаем из нуля разность – фактически инвертируем знак! ]
[70]	T 1 S [w2:] [ в ячейку со вторым числом кладем положительную разность, обнуляя аккумулятор ]
[71]	E 60 [<iter>] S [ аккумулятор=0 –> идем к следующей итерации поиска НОД]

[exit:]
[Модификация адресных полей изменяемых инструкций для следующего шага рабочего цикла]
[72]	T 0 S [обнуляем аккумулятор]
[73]	A 97 [<c1>] S [загрузка в аккумулятор константы 1]
[74]	L 0 L [сдвиг на 1 разряд влево]
[75]	A 61 [<r1>] S [прибавляем код инструкции, исполненной на предыдущем шаге]
[76]	U 61 [<r1>] S [записываем сформированную инструкцию в память, без обнуления аккумулятора]
[77]	T 94 [<r1>] S [записываем сформированную инструкцию в память]

[78]	A 97 [<c1>] S [загрузка в аккумулятор константы 1]
[79]	L 0 L [сдвиг на 1 разряд влево]
[80]	A 62 [<r2>] S [прибавляем код инструкции, исполненной на предыдущем шаге]
[81]	U 62 [<r2>] S [записываем сформированную инструкцию в память, без обнуления аккумулятора]
[82]	T 69 [<r2>] S [записываем сформированную инструкцию в память]

[83]	A 97 [<c1>] S [загрузка в аккумулятор константы 1]
[84]	L 0 L [сдвиг на 1 разряд влево]
[85]	A 64 [<w1>] S [прибавляем код инструкции, исполненной на предыдущем шаге]
[86]	T 64 [<w1>] S [записываем сформированную инструкцию в память]

[87]	A 97 [<c1>] S [загрузка в аккумулятор константы 1]
[88]	L 0 L [сдвиг на 1 разряд влево]
[89]	A 68 [<w2>] S [прибавляем код инструкции, исполненной на предыдущем шаге]
[90]	U 68 [<w2>] S [записываем сформированную инструкцию в память, без обнуления аккумулятора]
[91]	T 70 [<w2>] S [записываем сформированную инструкцию в память, обнуляя аккумулятор]

[92]	E 56 [<loop>] S [аккумулятор=0 -> повторяем рабочий цикл]

[end:]
[93]	T 0 S [Обнуляем аккумулятор]
[94]	A 0 S [r1:] [Загружаем в аккумулятор найденный НОД массива]
[95]	T 0 S [Записываем его в ячейку 0]
[96]	Z 0 S [останов ]

[c1:]
[97]	P 0 L [константа 1 (0...0|0|0...0|1)]

[len:]
[98]	P 4 S [длина массива (=8)]

[addr:]
[99]	P 50 S [адрес первой ячейки массива (=100)]
[array:]
[100]	P 4 L	[9]
[101]	P 9 S	[18]
[102]	P 15 S	[30]
[103]	P 30 S	[60]
[104]	P 60 S	[120]
[105]	P 18 S	[36]
[106]	P 16 L	[33]
[107]	P 12 S	[24]

